include ':app'

def localProps = new Properties()
def localPropsFile = file('local.properties')
if (localPropsFile.exists()) {
    localPropsFile.withInputStream { localProps.load(it) }
}

def cacheUrl = localProps.getProperty('gradle.buildCache.url')
def cacheUser = localProps.getProperty('gradle.buildCache.user')
def cachePass = localProps.getProperty('gradle.buildCache.pass')
def cachePush = localProps.getProperty('gradle.buildCache.push') == 'true'

println "Gradle cache: GRADLE_BUILD_CACHE_URL=${cacheUrl ?: '(not set)'}"

buildCache {
    local { enabled = true }
    if (cacheUrl) {
        remote(HttpBuildCache) {
            url = uri(cacheUrl)
            push = cachePush
            allowInsecureProtocol = true
            if (cacheUser && cachePass) {
                credentials {
                    username = cacheUser
                    password = cachePass
                }
            }
        }
    } else {
        println "Gradle cache: remote cache disabled (no URL)"
    }
}
